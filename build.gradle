/**
 * 1.0.0  : release
 * 1.1.x  : バージョン規則変更
 */
def ARTIFACT_VERSION = System.env.CIRCLE_TAG == null ?
        "1.1" : System.env.CIRCLE_TAG.substring(System.env.CIRCLE_TAG.indexOf('v') + 1)
def REPOSITORY_NAME = "maven"
def BINTRAY_LICENSES = ["MIT"]
def BINTRAY_LABELS = ["GAE/Go", "Golang", "Android", "Swagger"]
def BINTRAY_API_KEY = System.env.BINTRAY_API_KEY
def BINTRAY_GPG_PASS = System.env.BINTRAY_GPG_PASS

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'groovy'
sourceCompatibility = 1.7
targetCompatibility = 1.7
group = "com.eaglesakura"
version = ARTIFACT_VERSION + getArtifactVersionSuffix()

repositories {
    mavenCentral()
    maven { url "https://dl.bintray.com/eaglesakura/maven/" }
}


configurations {
    groovyDoc
}

dependencies {
    compile gradleApi()
    groovyDoc 'org.codehaus.groovy:groovy-groovydoc:2.3.3'
    groovyDoc 'org.codehaus.groovy:groovy-ant:2.3.3'
    compile 'commons-codec:commons-codec:1.10'
    testCompile group: 'junit', name: 'junit', version: '4.11'

    // Codegen main
    compile 'com.eaglesakura:java-commons:2.0.3'
    compile 'io.swagger:swagger-parser:1.0.25'
    compile 'io.swagger:swagger-compat-spec-parser:1.0.25'
    compile 'io.swagger:swagger-core:1.5.12'
    compile 'com.samskivert:jmustache:1.13'
    compile 'commons-io:commons-io:2.5'
    compile 'org.slf4j:slf4j-ext:1.7.24'
    compile 'org.slf4j:slf4j-api:1.7.24'
    compile 'org.slf4j:slf4j-simple:1.7.24'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'io.airlift:airline:0.7'
    compile 'com.googlecode.lambdaj:lambdaj:2.3.3'
}

apply plugin: 'maven'

groovydoc {
    groovyClasspath = configurations.groovyDoc
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from "${buildDir}/docs/groovydoc"
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

/**
 * バージョンを取得する
 * CircleCIの場合、バージョン末尾にビルド番号を付与する
 */
def getArtifactVersionSuffix() {
    def CIRCLE_TAG = System.env.CIRCLE_TAG

    if (CIRCLE_TAG != null) {
        // TAGがある場合それを直接利用する
        return ""
    }

    if (System.env.CIRCLE_BUILD_NUM != null) {
        // CircleCIのバージョンが指定されているので、そちらを利用する
        return ".build-${System.env.CIRCLE_BUILD_NUM}"
    } else {
        return ".snapshot"
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = 'eaglesakura'
    key = BINTRAY_API_KEY
    pkg {
        repo = REPOSITORY_NAME
        name = file(".").absoluteFile.name
        licenses = BINTRAY_LICENSES
        labels = BINTRAY_LABELS
        issueTrackerUrl = "https://github.com/eaglesakura/${file(".").absoluteFile.name}/issues"
        vcsUrl = "https://github.com/eaglesakura/${file(".").absoluteFile.name}"
        version {
            name = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = BINTRAY_GPG_PASS
            }
        }
    }
    publications = ['Publication']
    configurations = ['archives']
}
